1 RUN 9000
10 FOR N=F TO F+G-1: PRINT OVER 0;AT N,E;B$( TO H): NEXT N
15 LET E=E*8: LET G=G*8: LET H=H*8: LET F=F*8
20 PLOT E,175-F: DRAW H,0: DRAW 0,-G: DRAW -H,0: DRAW 0,G: IF MN THEN PLOT E+H+1,174-F: DRAW 0,-G: DRAW -H,0
21 RETURN 
100 LET x=x+(j*(IN 57342=190))-(j*(IN 57342=189))
110 LET y=y+(j*(IN 64510=190))-(j*(IN 65022=190))
115 LET y=y+(-j AND y>175)+(j AND y<0): LET x=x+(j AND x<0)+(-j AND x>255)
120 IF IN 57342=187 THEN BEEP .1,10: GO TO 8100
125 IF IN 63486=190 THEN BEEP .1,10: GO SUB 600
130 IF IN 32766=190 THEN GO SUB 300
135 IF IN 61438=190 THEN PLOT OVER 1;X,Y: BEEP .1,0
140 IF IN 49150=187 THEN GO TO 250
145 IF IN 49150=183 THEN GO TO 245
150 IF x=a AND y=b THEN GO TO 100

160 PLOT INVERSE R; OVER L;a,b
170 LET a=x: LET b=y: PLOT OVER 1;a,b
180 GO TO 100
245 LET J=J-1: IF J=0 THEN LET J=10
246 GO TO 260
250 LET j=j+1: IF J=11 THEN LET J=1
260 BEEP .05,J*2
265 PRINT #0;AT 1,0;"SPEED:";J;" ";AT 1,9;":";("   LINE  " AND F1);("   BOX   " AND F2);("  CIRCLE  " AND F3);(" MAGNIFY " AND F4);("  RUBBER " AND F5);("SPACE OFF" AND F1+F2+F3+F4+F5=0)
266 PRINT #0;AT 1,24;":      ";AT 1,25;("RUBBER" AND R+RU=2 AND L=0);("PENCIL" AND RU+R+L=0)
270 GO TO 130
280 LET L=ABS (L-1): BEEP .1,L*5: GO SUB LO
285 IF L=1 THEN LET RU=0
290 GO TO 260
300 IF F1=1 THEN LET XL=X: LET YL=Y: BEEP .1,10: PLOT OVER 1;X,Y: LET F1=2: RETURN 
305 IF F1=2 THEN PLOT XL,YL: DRAW (X-XL),(Y-YL): BEEP .1,0: PLOT OVER 1;X,Y: LET F1=1: RETURN 
310 IF F2=2 THEN PLOT XL,YL: DRAW (X-XL),0: DRAW 0,(Y-YL): DRAW -(X-XL),0: DRAW 0,-(Y-YL): PLOT OVER 1;X,Y: BEEP .1,0: LET F2=1: RETURN 
315 IF F2=1 THEN LET XL=X: LET YL=Y: BEEP .1,10: PLOT OVER 1;X,Y: LET F2=2: RETURN 
320 IF F3=1 THEN LET XL=X: LET YL=Y: PLOT OVER 1; X,Y: BEEP .1,10: LET F3=2: RETURN 
330 IF F3=2 THEN GO TO 380
331 IF F4=1 THEN GO TO 4000
332 IF F5=1 THEN IF RU=0 THEN LET R=1: LET LS=L: LET L=0: LET RU=1:  GO TO 260
333 IF F5=1 THEN LET RU=0: LET R=0: LET L=LS: GO TO 260
335 RETURN 
380 LET R1=ABS (X-XL): LET R2=ABS (Y-YL): LET DS=SQR ((R1*R1)+(R2*R2))
385 IF XL+DS>255 OR XL-DS<0 THEN RETURN 
390 IF YL+DS>175 OR YL-DS<0 THEN RETURN 
395 CIRCLE XL,YL,DS: PLOT OVER 1;X,Y: LET F3=1: RETURN 
500 LET MN=0: FOR Z=0 TO 7: FOR O=6 TO 9: PRINT AT O,3+Z*3; PAPER Z;"  ": NEXT O: PRINT AT 11,3+Z*3;Z: NEXT Z: LET MN=1: RETURN 
510 LET MN=0: FOR Z=0 TO 7: FOR O=9 TO 12: PRINT AT O,6+Z*3; PAPER Z;"  ": NEXT O: PRINT AT 14,6+Z*3;Z: NEXT Z: LET MN=1: RETURN 
600 FOR N=0 TO 1: PLOT OVER 1;A,1: DRAW OVER 1;0,174: PLOT OVER 1;1,B: DRAW OVER 1;253,0: PAUSE 1: PAUSE N: NEXT N: RETURN 
610 FOR M=8 TO 248 STEP 8: PLOT OVER 1;M,0: DRAW OVER 1;0,175: NEXT M: FOR Z=8 TO 168 STEP 8: PLOT OVER 1;0,Z: DRAW OVER 1;255,0: NEXT Z: RETURN 
4000 LET A=X: LET B=Y
4010 IF X<11 THEN LET X=11
4011 IF X>244 THEN LET X=244
4012 IF Y>164 THEN LET Y=164
4013 IF Y<11 THEN LET Y=11
4014 LET XS=X-10: LET XF=X+10: LET YS=Y-10: LET YF=Y+10
4015 GO SUB SV: PLOT XS-1,YS-1: DRAW 22,0: DRAW 0,22: DRAW -22,0: DRAW 0,-22
4016 GO SUB COLOUR: BEEP .1,10: LET E=0: LET F=0: LET G=3: LET H=16: GO SUB 10
4017 PRINT AT 1,1;"MAGNIFY AREA?"
4018 LET A$=INKEY$: IF A$="" THEN GO TO 4018
4019 IF A$=" " OR A$="Y" THEN GO TO 4050
4020 POKE 23693,COLOURO: GO SUB LO: GO TO 130
4050 GO SUB LO: PLOT OVER 1;X,Y: GO SUB 5000
4055 LET X=A: LET Y=B: PLOT OVER 1;X,Y: GO TO 130
5000 BEEP .1,20: REM 21 BY 21 GRABBER
5005 INPUT "GET CONTENTS?"; LINE A$: IF A$="N" THEN DIM W(21,21): GO TO 5020
5010 DIM W(21,21): PRINT #0; FLASH 1;AT 1,28;"WAIT": FOR N=XS TO XF: FOR M=YS TO YF: LET W(N-XS+1,21-M+YS)=POINT (N,M): NEXT M: NEXT N
5020 CLS : IF A$<>"N" THEN PRINT AT 10,23;"PLEASE";AT 11,24;"WAIT"
5025 FOR N=0 TO 160 STEP 8: PLOT 8,N: DRAW 167,0: PLOT N+8,0: DRAW 0,167: NEXT N: PLOT 8,168: DRAW 168,0: DRAW 0,-168: PLOT 192,135: DRAW 22,0: DRAW 0,-22: DRAW -22,0: DRAW 0,22
5030 IF A$<>"N" THEN FOR N=1 TO 21: FOR M=1 TO 21: PRINT AT M,N; OVER 1;("\::" AND W(N,M)=1): PLOT INVERSE 1-(W(N,M));192+N,135-M: NEXT M: NEXT N
5031 PRINT AT 10,23;"R=END ";AT 11,24;"    "
5040 LET X=1: LET Y=1
5045 PRINT AT Y,X; OVER 1;"+": LET X1=X: LET Y1=Y
5050 LET X=X+(INKEY$="P" AND X<21)-(INKEY$="O" AND X>1): LET Y=Y+(INKEY$="A" AND Y<21)-(INKEY$="Q" AND Y>1)
5055 IF INKEY$=CHR$ 12 THEN PRINT AT Y,X; OVER 1;"\::": PLOT OVER 1;192+X,135-Y: LET W(X,Y)=ABS (W(X,Y)-1): BEEP .1,0
5060 IF INKEY$= " " THEN IF W(X,Y)=0 THEN LET W(X,Y)=1: PLOT 192+X,135-Y: PRINT AT Y,X; OVER 1;"\::": BEEP .1,0
5065 IF INKEY$="R" THEN GO TO 5200
5070 IF X=X1 AND Y=Y1 THEN GO TO 5050
5075 PRINT AT Y1,X1; OVER 1;"+": GO TO 5045
5210 POKE 23693,COLOURO: GO SUB LO: FOR N=XS TO XF: FOR M=YS TO YF: PLOT INVERSE (1- W(N-XS+1,21-M+YS)); N,M: NEXT M: NEXT N
5215 BEEP .1,10: RETURN 
6000 REM NEW STUFF HERE
6010 GO SUB LO: GO TO 130
7910 LET CLL=USR 45000: RETURN 
7920 LET CLL=USR 45024: RETURN 
7925 LET CLL=USR 45012: RETURN 
8010 GO SUB COLOUR: LET E=9: LET F=3: LET G=16: LET H=19: GO SUB WI
8020 PRINT AT 4,11;"SELECT FUNCTION";AT 6,10;"1 LINE";AT 8,10;"2 BOX";AT 10,10;"3 CIRCLE";AT 12,10;"4 MAGNIFY AREA";AT 14,10;"5 RUBBER";AT 16,10;"6 FUNCTIONS OFF": POKE 23693,COLOURO
8030 LET F1=0: LET F5=0: LET F2=0: LET R=0: LET F3=0: LET F4=0
8040 LET A$=INKEY$: IF A$<"1" OR A$>"6" THEN GO TO 8040
8050 IF A$="1" THEN LET F1=1
8055 IF A$="2" THEN LET F2=1
8060 IF A$="3" THEN LET F3=1
8065 IF A$="4" THEN LET F4=1
8066 IF A$="5" THEN LET F5=1
8070 GO SUB LO: GO TO 260
8100 GO SUB SV: GO SUB COLOUR: REM ICONS
8110 LET E=1: LET F=2: LET G=6: LET H=30: GO SUB WI
8115 POKE 23606,248: POKE 23607,166
8120 PRINT AT 5,2;"$%\   !\  ()\  ,-\  01\  89\  ";CHR$ 60+CHR$ 61;"\  PQ\  TU\  XY"
8130 PRINT AT 6,2;"&'\  ""#\  *+\  ./\  23\  :;\  >?\  RS\  VW\  Z";CHR$ 91
8135 POKE 23606,16: POKE 23607,163: PRINT AT 3,9;"ICON SELECTION:"
8136 IF DEMO THEN RETURN 
8140 PRINT OVER 1;AT 4,i;"\n"
8150 LET i=i+3*(IN 57342=190 AND i<27)-3*(IN 57342=189 AND i>2)
8160 IF (IN 64510=190) OR (IN 57342=187) THEN POKE 23693,COLOURO: GO SUB LO: GO TO 130
8170 IF IN 32766=190 THEN POKE 23693,COLOURO: GO TO G((I+2)/3)
8175 IF I=K THEN GO TO 8150
8190 PRINT OVER 1;AT 4,K;"\n": LET K=I: GO TO 8140
8200 GO SUB COLOUR: LET E=3: LET F=6: LET G=15: LET H=27: GO SUB WI
8210 PRINT AT 7,6;"BASIC MENU OF OPTIONS"
8220 PRINT AT 10,5;"1 ATTRIBUTE GRID ON,OFF";AT 12,5;"2 DISPLAY KEYS";AT 14,5;"3 +3 DOS";AT 16,5;"4 SUITE PACKAGE MENU";AT 18,5;"5 RETURN": POKE 23693,COLOURO
8225 LET A$=INKEY$: IF A$="" THEN GO TO 8225
8226 IF A$="4" THEN CLEAR : LOAD "DISK"
8230 IF A$="1" THEN GO SUB LO: GO SUB 610: GO TO 130
8235 IF A$="5" THEN GO SUB LO: GO TO 130
8236 IF A$="2" THEN GO TO 8300
8237 IF A$="3" THEN RUN 10000
8240 GO TO 8225
8300 GO SUB COLOUR: LET E=1: LET F=1: LET G=19: LET H=25: GO SUB WI
8310 PRINT AT 2,3;"KEYBOARD CONTROL LIST";AT 4,2;"MAIN PROGRAM:"
8315 PRINT AT 5,2;"Q-   UP         A- DOWN";AT 6,2;"O- LEFT         P-RIGHT"
8320 PRINT AT 8,2;"SPACE OPERATES FUNCTION";AT 10,2;"J-SPEED DOWN K-SPEED UP";AT 11,2;"DEL.-SET OR RESET POINT";AT 12,2;"1-CURSOR LOCATE I-ICONS"
8325 PRINT AT 14,2;"SUBPROGRAMS:";AT 15,2;"ENTER-ACTION KEY";AT 16,2;"R-REDRAW SCREEN"
8330 PRINT AT 18,2;"SEE MANUAL FOR DETAILS"
8335 IF INKEY$<>" " THEN GO TO 8335
8340 POKE 23693,COLOURO: GO SUB LO: GO TO 260
8399 STOP 
8410 LET C$="TEXT": LET ALL=28: GO SUB 8500
8415 GO SUB LO
8420 PAPER 8: INK 8: BRIGHT 8
8425 LET LI=LEN F$: LET X1=0: LET Y1=0
8430 PRINT AT Y1,X1; OVER 1;F$: LET Y2=Y1: LET X2=X1
8435 LET Y1=Y1+(INKEY$="A" AND Y1<21)-(INKEY$="Q" AND Y1>0): LET X1=X1+(INKEY$="P" AND X1+LI<32)-(INKEY$="O" AND X1>0)
8436 IF INKEY$=" " THEN GO TO 8450
8437 IF INKEY$=CHR$ 13  THEN PRINT AT Y1,X1; OVER 1;F$: INK IK: PAPER PR: BRIGHT BR: GO TO 130
8438 IF X2=X1 AND Y2=Y1 THEN GO TO 8435
8440 PRINT AT Y2,X2; OVER 1;F$
8445 GO TO 8430
8455 PRINT AT Y1,X1; OVER 1;F$
8460 GO TO 8435
8500 GO SUB COLOUR: LET F=1: LET E=1: LET G=5: LET H=30: GO SUB 10
8510 POKE 23658,0: PRINT AT 2,2;"INPUT ";C$;" MAX ";ALL;" CHARS"
8520 LET F$="": LET POS=0
8525 PRINT AT 4,2;"_";CHR$ 8;
8530 LET C$=INKEY$
8535 IF C$=CHR$ 13 THEN POKE 23658,8: POKE 23693,COLOURO: RETURN 
8540 IF C$=CHR$ 12 THEN IF POS>0 THEN LET POS=POS-1: LET F$=F$( TO POS): PRINT " ";CHR$ 8;CHR$ 8;: GO TO 8560
8545 IF POS>ALL THEN GO TO 8530
8546 IF  CODE C$<32 OR  CODE C$>122 THEN GO TO 8530
8550 LET F$=F$+C$: LET POS=POS+1
8555 PRINT C$;
8560 PRINT "_";CHR$ 8;: BEEP .05,0
8565 GO TO 8530
8600 GO SUB COLOUR: LET f=1: LET e=9: LET h=21: LET g=20: GO SUB WI
8610 PRINT AT 2,11;"DISK CONTROLLER"
8620 PRINT AT 5,10;"1 IMPORT SCREEN";AT 7,10;"2 EXPORT SCREEN";AT 9,10;"3 IMPORT TEXTURES";AT 11,10;"4 MERGE 9900 ROUTINE";AT 13,10;"5 CALL 9900 ROUTINE";AT 15,10;"6 LOAD FONT (42000)";AT 17,10;"7 CAT DRIVE A";AT 19,10;"8 RETURN": POKE 23693,COLOURO
8625 LET A$=INKEY$: IF A$<"1" OR A$>"8" THEN GO TO 8625
8626 IF A$="8" THEN GO SUB LO: GO TO 260
8627 IF A$="7" THEN GO TO 8680
8628 IF A$="5" THEN GO SUB LO: GO TO 9900
8630 LET C$="FILENAME": LET ALL=12: GO SUB 8500
8632 IF A$="1" THEN CLS : LOAD ("A:"+F$) SCREEN$ : GO TO 260
8635 IF A$="2" THEN GO SUB LO: PRINT #0;AT 0,0;B$;B$: SAVE ("A:"+F$) SCREEN$ : GO TO 260
8640 IF A$="3" THEN LOAD "A:"+F$ CODE : GO TO 260
8645 IF A$="4" THEN MERGE "A:"+F$: GO SUB LO: GO TO 260
8650 IF A$="6" THEN LOAD "A:"+F$ CODE 42000,768: GO SUB LO: GO TO 260
8655 GO TO 8625
8680 CLS : POKE 23692,255: CAT "A:"
8685 IF INKEY$<>" " THEN GO TO 8685
8690 GO SUB LO: GO TO 8600
8700 GO SUB COLOUR: LET F=3: LET E=2: LET H=25: LET G=10: GO SUB WI
8710 GO SUB 500
8720 PRINT AT 4,9;"SELECT INK"
8725 LET A$=INKEY$: IF A$<"0" OR A$>"7" THEN GO TO 8725
8730 LET IK=VAL A$
8740 LET F=6: LET E=5: LET H=25: LET G=10: GO SUB WI
8745 GO SUB 510
8750 PRINT AT 7,10;"SELECT PAPER"
8755 LET A$=INKEY$: IF A$<"0" OR A$>"7" THEN GO TO 8755
8760 LET PR=VAL A$
8765 LET E=8: LET F=10: LET G=10: LET H=15: GO SUB WI
8766 PRINT AT 11,9;"SELECT BRIGHT"
8770 FOR Z=0 TO 1: FOR O=13 TO 16: PRINT AT O,13+(Z*3); BRIGHT Z;"  ": NEXT O: PRINT AT 18,13+Z*3;Z: NEXT Z
8775 LET A$=INKEY$: IF A$<"0" OR A$>"1" THEN GO TO 8775
8780 LET BR=VAL A$
8790 LET E=3: LET F=1: LET G=17: LET H=24: GO SUB WI
8791 LET COLOURN=IK+8*PR+64*BR
8795 PRINT AT 2,5;"COLOURING OPTIONS";AT 5,4;"1 REDRAW SCREEN WITH";AT 6,6;"NEW COLOURS";AT 8,4;"2 DRAW WITH OLD THEN";AT 9,6;"SWITCH TO NEW";AT 11,4;"3 ABANDON SELECTION": POKE 23693,COLOURN
8800 LET A$=INKEY$: IF A$<"1" OR A$>"3" THEN GO TO 8800
8805 IF A$="1" THEN GO SUB 9260: GO SUB LO1: GO TO 260
8810 IF A$="2" THEN GO SUB LO: GO TO 130
8815 GO TO 130
8850 GO SUB COLOUR: LET F=10: LET E=10: LET G=3: LET H=16: GO SUB WI
8860 PRINT AT 11,11;"CONFIRM TRASH?": POKE 23693,COLOURO
8865 LET Z$=INKEY$: IF Z$ ="" THEN GO TO 8865
8867 IF Z$="Y" OR z$="y" THEN CLS : GO SUB 9260: GO TO 260
8870 GO SUB LO: GO TO 130
8900 GO SUB COLOUR: LET F=2: LET E=3: LET G=4: LET H=25: GO SUB WI
8905 PRINT AT 3,4;"CONFIRM FILL FROM POINT": POKE 23693,COLOURO
8906 LET A$=INKEY$: IF A$="" THEN GO TO 8906
8907 IF A$ = "Y" THEN GO TO 8910
8908 GO SUB LO: GO TO 130
8910 GO SUB LO: PLOT X,Y
8915 LET DMY=USR 64800
8920 BEEP .1,0: PLOT OVER 1;X,Y: GO TO 130
8930 REM COLOUR
8940 LET COLOURO=PEEK 23693
8945 POKE 23693,56: RETURN 
9000 IF PEEK 43001<>1 THEN LOAD "UTILS" CODE 45000: LOAD "FONT" CODE 42000: LOAD "ICONS" CODE 43000: LOAD "FILL" CODE 64800: LOAD "INPUTS" CODE 63000
9001 CLEAR 58000: LET J=1
9002 LET RU=0: LET R=0: LET MN=1: LET WI=10: LET SV=7910: LET LO1=7920: LET LO=7925
9005 LET F5=0: LET IK=7: LET F4=0: POKE 23658,8: LET F3=0: LET F1=0: LET F2=0: LET L=1: LET PR=0: LET BR=1
9010 LET DEMO=0: LET COLOUR=8930: LET x=128: LET y=88
9015 DIM b$(32): DIM g(10)
9020 POKE 23607,163: POKE 23606,16: POKE 23675,104: POKE 23676,170: LET a=x: LET i=2: LET K=i: LET b=y
9100 RESTORE 9110: FOR n=1 TO 10: READ g(n): NEXT n
9110 DATA 8600,9400,8200,8400,280,8900,6000,8700,8000,8850
9256 BORDER 0: PAPER PR: INK IK: BRIGHT BR: LET RETU=0
9290 CLS : PLOT 0,0: DRAW 255,0: DRAW 0,175: DRAW -255,0: DRAW 0,-175
9296 IF RETU THEN RETURN 
9297 LET RETU=1
9370 PLOT OVER 1;A,B: GO TO 260
9400 REM HELP
9410 GO SUB COLOUR: LET E=5: LET F=7: LET G=13: LET H=20: GO SUB WI
9415 PRINT AT 8,9;"HELP OPTIONS:";AT 11,6;"1 ICONS";AT 13,6;"2 FUNCTIONS";AT 15,6;"3 FONTS,TEXTURES";AT 17,6;"4 RETURN": POKE 23693,COLOURO
9420 LET A$=INKEY$: IF A$<"1" OR A$>"4" THEN GO TO 9420
9425 IF A$="4" THEN GO SUB LO: GO TO 260
9430 IF A$="1" THEN GO TO 9450
9435 IF A$="2" THEN GO TO 9510
9436 GO TO 9550
9450 GO SUB COLOUR: LET E=2: LET F=1: LET G=15: LET H=28: GO SUB WI
9460 LET DEMO=1: GO SUB 8110: LET DEMO=0
9462 PRINT AT 10,3;"THIS ICON CALLS:";AT 14,13;"SPACE TO PROCEED"
9465 RESTORE 9500: FOR N=2 TO 29 STEP 3
9466 PRINT AT 4,N;"\n";AT 7,N;"\l"
9470 READ A$: PRINT AT 12,3;"                           ": PRINT AT 12,2+((26-LEN A$)/2);"""";A$;"""": BEEP .1,5
9485 PAUSE 1: IF INKEY$<>" " THEN GO TO 9480
9490 PRINT OVER 1;AT 4,N;"\n";AT 7,N;"\l"
9495 NEXT N: POKE 23693,COLOURO: GO TO 9400
9500 DATA "DISK BASED OPERATIONS","INFORMATION PAGES","BASIC MENU","TEXT OPERATIONS","PENCIL DOWN OR UP","FILL & TEXTURE SHAPE","CHANGE PENCIL NIBS","COLOUR PALETTE","FUNCTIONS & SHAPES","TRASH CAN"
9510 GO SUB COLOUR: LET E=2: LET F=1: LET G=20: LET H=29: GO SUB WI
9515 PRINT AT 2,5;"FUNCTION PAGE"
9520 PRINT AT 4,3;"AFTER YOU HAVE SELECTED THE";AT 5,3;"FUNCTION YOU WANT , MOVE TO";AT 6,3;"THE POSITION YOU WANT IT TO";AT 7,3;"WORK FROM . IF THE FUNCTION";AT 8,3;"HAS ONLY ONE PARAMETER , IT";AT 9,3;"WILL NOW SWITCH ON."
9525 PRINT AT 10,3;"  IF THE FUNCTION NEEDS TWO";AT 11,3;"PARAMETERS,THEN MOVE TO THE";AT 12,3;"OTHER POINT AND PRESS SPACE";AT 13,3;"ONCE AGAIN.NOW IT WILL CALL"
9530 PRINT AT 15,3;"1 PARAMETER:rubber,magnify.";AT 17,3;"2 PARAMETER:line,circle,box"
9535 PRINT AT 19,5;"SPACE FOR HELP PAGE"
9536 IF INKEY$<>" " THEN GO TO 9536
9540 POKE 23693,COLOURO: GO TO 9400
9550 GO SUB COLOUR: LET E=3: LET F=4: LET G=14: LET H=27: GO SUB WI
9555 PRINT AT 5,5;"FONTS AND TEXTURES"
9560 PRINT AT 7,4;"FONTS:";AT 9,4;"FONTS SHOULD BE STORED AS";AT 10,4;"A SEQUENTIAL FILE OF  768";AT 11,4;"BYTES, AS A SPECTRUM CHAR";AT 12,4;"SET (SPACE TO \*). ARTIST";AT 13,4;"WILL LOAD IT INTO ITS OWN";AT 14,4;"RESERVED ADDRESS.";AT 16,4;"PRESS THE SPACE BAR"
9565 PAUSE 1: IF INKEY$<>" " THEN GO TO 9565
9570 LET E=3: LET F=4: LET G=14: LET H=27: GO SUB WI
9575 PRINT AT 5,5;"FONTS AND TEXTURES"
9580 PRINT AT 7,4;"TEXTURES:";AT 9,4;"TEXTURES ARE STORED AS AN";AT 10,4;"EIGHT BYTE PATTERN,LIKE A";AT 11,4;"UDG,SEQUENTIALLY,IN BANKS";AT 12,4;"IN RAM. IT IS POSSIBLE TO";AT 13,4;"HOLD UP TO 48 PATTERNS AT";AT 14,4;"ANY ONE TIME. ARTIST WILL";AT 15,4;"POSITION THE DATA ITSELF,";AT 16,4;"BUT KEEP TO THE 48 LIMIT."
9585 PAUSE 1: IF INKEY$<>" " THEN GO TO 9585
9590 POKE 23693,COLOURO: GO TO 9400
9900 REM KEMPSTON MOUSE
9901 REM X = IN 64479
9902 REM Y = IN 65503 (255)
9903 REM LMB = IN 64223=253
9904 REM RMB = IN 64223=254
9998 STOP 
9999 SAVE "ARTIST" LINE 1: SAVE "UTILS" CODE 45000,35: SAVE "FONT" CODE 42000,868: SAVE "ICONS" CODE 43000,912: SAVE "FILL" CODE 64800,500: SAVE "INPUTS" CODE 63000,313: RUN 
